buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    compile project(path: ':backend', configuration: 'android-endpoints')
    compile project(path: ':common')
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.wefika:flowlayout:0.4.1'
    compile 'com.github.lecho:hellocharts-library:1.5.8@aar'
    compile 'joda-time:joda-time:2.9.2'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-simplexml:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.google.dagger:dagger:2.6'
    apt 'com.google.dagger:dagger-compiler:2.6'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.google.android.gms:play-services-base:9.4.0'
    compile 'com.google.android.gms:play-services-auth:9.4.0'
    compile 'com.google.android.gms:play-services-gcm:9.4.0'
    compile 'com.google.android.gms:play-services-analytics:9.4.0'
    compile 'com.google.protobuf:protobuf-java:2.6.1'
    compile 'com.birbit:android-priority-jobqueue:2.0.0-alpha2'
    compile 'org.greenrobot:eventbus:3.0.0'
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    signingConfigs {
        config {
            keyAlias 'optionfusion'
            keyPassword '$System.env.SIGNPW'
            storeFile file('/Users/jimkerr/Dropbox/Private/MomentumSupport/keystore.jks')
            storePassword '$System.env.SIGNPW'
        }
        debugConfig {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('/Users/jimkerr/.android/debug.keystore')
            storePassword 'android'
        }
    }

    defaultConfig {
        applicationId "com.optionfusion"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 9
        versionName "0.9"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        /**
         * Defines whether authentication is required or not. Only works once
         * deployed on production AppEngine, not on local server.
         */
        buildConfigField "boolean", "SIGN_IN_REQUIRED", "true"
        /**
         * Substitute you own sender ID here. This is the project number you got
         * from the API Console, as described in "Getting Started."
         */
        buildConfigField "String", "SENDER_ID", "\"386969647168\""
        /**
         * Web client ID from Google Cloud console.
         */
        buildConfigField "String", "WEB_CLIENT_ID", "\"386969647168-q03ig3s5jmp26onj5bfn0bobuvpatdm2.apps.googleusercontent.com\""
        signingConfig signingConfigs.config
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.debugConfig
        }
    }

    configurations {
        compile.exclude group: 'stax'
        compile.exclude group: 'xpp3'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        local {
            buildConfigField "String", "ROOT_URL",
                    "\"http://192.168.1.143:8080/_ah/api\""
        }
        cloud {
            buildConfigField "String", "ROOT_URL",
                    "\"https://option-fusion-api.appspot.com/_ah/api/\""
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

apply plugin: 'com.google.gms.google-services'
